private txt:=split(
//!~
 1. "This is a numbered list of twelve statements."
 2. "Exactly 3 of the last 6 statements are true."
 3. "Exactly 2 of the even-numbered statements are true."
 4. "If statement 5 is true, then statements 6 and 7 are both true."
 5. "The 3 preceding statements are all false."
 6. "Exactly 4 of the odd-numbered statements are true."
 7. "Either statement 2 or 3 is true, but not both."
 8. "If statement 7 is true, then 5 and 6 are both true."
 9. "Exactly 3 of the first 6 statements are true."
10. "The next two statements are both true."
11. "Exactly 1 of statements 7, 8 and 9 are true."
12. "Exactly 4 of the preceding statements are true."~,"\n");

private
check(T:BooleanList(13))->T==[false,
                          true,
                          trueCount(T[[7..12]])=3,
                          trueCount(T[[2,4,6,8,10,12]])=2,
                          not(T[5]) OR T[6] AND T[7],
                          not(T[[2,3,4]].agg(OR)),
                          T[[1,3,5,7,9,11]].trueCount=4,
                          T[2] xor T[3],
                          not(T[7]) OR T[5] AND T[6],
                          T[[1..6]].trueCount=3,
                          T[[11,12]].agg(AND),
                          T[[7,8,9]].trueCount=1,
                          T[[1..11]].trueCount=4];
private printBits(i)->printf("%S",[0..11].each(j,txt[j]&"\tis "&((i and (2^j))>0)));

private twelveBits(i)->false|bits(i).head(12);
//*See http://rosettacode.org/wiki/Twelve_statements
main(...)->[0..2^12-1].pEach(i,check(twelveBits(i)) ? printBits(i) orElse true : false,OR);
