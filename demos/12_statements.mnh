private txt:=split(
//!BLOB START
 1. "This is a numbered list of twelve statements."
 2. "Exactly 3 of the last 6 statements are true."
 3. "Exactly 2 of the even-numbered statements are true."
 4. "If statement 5 is true, then statements 6 and 7 are both true."
 5. "The 3 preceding statements are all false."
 6. "Exactly 4 of the odd-numbered statements are true."
 7. "Either statement 2 or 3 is true, but not both."
 8. "If statement 7 is true, then 5 and 6 are both true."
 9. "Exactly 3 of the first 6 statements are true."
10. "The next two statements are both true."
11. "Exactly 1 of statements 7, 8 and 9 are true."
12. "Exactly 4 of the preceding statements are true."
//!BLOB END
,"\n");

private
check(T:booleanList)->T==[false,
                          true,
                          trueCount(T.get([7..12]))=3,
                          trueCount(T.get([2,4,6,8,10,12]))=2,
                          not(T.get(5)) OR T.get(6) AND T.get(7),
                          not(T.get([2,3,4]).listOr),
                          T.get([1,3,5,7,9,11]).trueCount=4,
                          T.get(2) xor T.get(3),
                          not(T.get(7)) OR T.get(5) AND T.get(6),
                          T.get([1..6]).trueCount=3,
                          T.get([11,12]).listAnd,
                          T.get([7,8,9]).trueCount=1,
                          T.get([1..11]).trueCount=4];
private printBits(i)->begin printf("%S",[0..11].each(j,txt.get(j)&"\tis "&((i and (2^j))>0))); true; end;

private bits(i)->false|(2^[0..11]).each(j,(i and j)>0);
//*See <a href="http://rosettacode.org/wiki/Twelve_statements">http://rosettacode.org/wiki/Twelve_statements</a>
main->[0..2^12-1].pEach(i,check(bits(i)) ? printBits(i) : false,OR);

