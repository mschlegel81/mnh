@demo_for=formatting, date/time formatting
private bigDigit:=
[[' ### ','#   #','#   #','#   #',' ### '],
 ['  # ',' ## ','# # ','  # ',' ###'],
 [' ### ','#   #','  ## ',' #   ','#####'],
 [' ### ','#   #','   # ','#   #',' ### '],
 ['  #  ',' #  #','#####','    #','    #'],
 ['#####','#    ','#### ','    #','#### '],
 [' ### ','#    ','#### ','#   #',' ### '],
 ['#####','   # ','  #  ','  #  ','  #  '],
 [' ### ','#   #',' ### ','#   #',' ### '],
 [' ### ','#   #',' ####','    #',' ### ']];

private getWeeks(year,month)->begin
  firstDayOfMonth:=parseTime('dd.mm.yyyy',format('01.%2.2d.%4.4d',month,year)).round;
  lastDayOfMonth :=parseTime('dd.mm.yyyy',month=12
                   ? format('01.%2.2d.%4.4d',      1,year+1)
                   : format('01.%2.2d.%4.4d',month+1,year  )).round-1;
  startingMonday :=firstDayOfMonth;
  while startingMonday mod 7 <> 2 do startingMonday-=1;
  daysOnTab:=[startingMonday..lastDayOfMonth]-firstDayOfMonth+1;
  daysOnTab
  .group([0..size(daysOnTab)-1] div 7)
  .getInner(1)
  .sort({$x[0]<$y[0]})
  .each(week,week.each(day,day<=0 ? '  ' : format('%2s',day)).join(' '));
end;

private printCalendar(year)->begin
  digitIndexes:=toString(year).chars.softCast;
  bigYear:=[0..4].each(i,bigDigit[digitIndexes].getInner(i).join(' '));
  bigYear:=repeatString(' ',(91-bigYear[0].length) div 2)&bigYear;
  print('+',repeatString('-',91),'+');
  printf('|%-91s|',bigYear);

  weeks:=[1..12].each(month,getWeeks(year,month));
  lineFormat:='| %-20s | %-20s | %-20s | %-20s |';
  splitLine:='+'&repeatString(repeatString('-',22)&'+',4);
  nameOfMonth:=['Januar','Februar','Maerz','April','Mai','Juni','Juli','August','September','Oktober','November','Dezember'];
  weekDays:='Mo Di Mi Do Fr Sa So';
  ([0..2]*4).each(leftMonth,
    print(splitLine),
    printf@(lineFormat | nameOfMonth[[leftMonth..leftMonth+3]]),
    printf(lineFormat,weekDays,weekDays,weekDays,weekDays),
    [0..5].each(i,printf(lineFormat,weeks[leftMonth  ,i] orElse "",
                                    weeks[leftMonth+1,i] orElse "",
                                    weeks[leftMonth+2,i] orElse "",
                                    weeks[leftMonth+3,i] orElse "")));
  print(splitLine);
end;

//*Prints a calendar of the given year
main(year)->printCalendar(year.softCast);
