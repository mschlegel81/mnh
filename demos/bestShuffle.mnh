private bruteForceShuffle(s:String)->begin
  local c:=s.chars;
  local minEq:=s.length;
  local best:=s;
  c.permutationIterator.each(p,begin
    local eq:=trueCount(p=c);
    eq<minEq
    ? begin
        minEq:=eq;
        best:=p.join;
      end
    : void;
    minEq=0;
  end,OR);
  best;
end;

private shuffleInfo(s:String)->begin
  local shuffled:=s.bruteForceShuffle;
  format("%s\v,\t%s\v,\t(%d)",s,shuffled,trueCount(s.chars=shuffled.chars));
end;

private inputs:=['abracadabra', 'seesaw', 'elk', 'grrrrrr', 'up', 'a'];
//*See http://rosettacode.org/wiki/Best_shuffle
main->main@inputs;
//*Return shuffle info on given input strings
main(...)->map(...,::shuffleInfo).join("\n").print;
