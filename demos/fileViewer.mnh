USE customFormUtil;

private datastore fileHlAssoc:=[].toMap;
private synchronized getHlAssoc(filename:String)->
  fileHlAssoc[filename] orElse filename.extractFileExt.replace('.','');
private synchronized logHlAssoc('',assoc)->void;
private synchronized logHlAssoc(filename,assoc)->
  filename.extractFileExt.replace('.','')==assoc
  ? void
  : fileHlAssoc[filename]:=assoc;

main->main('');

//@demo_for=custom forms
main(fileToOpen)->begin
  local filename:='';
  local lines   :='';
  local hl      :='txt';
  local openFile:={fileExists($filename)
    ? begin
        logHlAssoc(filename,hl);
        filename:=$filename;
        lines:=filename.fileLines.join("\n");
        hl:=filename.getHlAssoc;
      end
    : begin
        logHlAssoc(filename,hl);
        lines:='';
        hl:='txt';
      end};
  openFile(fileToOpen.expandedFileName);
  showDialog('Fileviewer',
            [newSplitPanel([newEdit
                              .bind('filename')
                              .withCaption('File')
                              .withAction(openFile)],
                           [newButton
                              .withCaption('...')
                              .withAction({openFile(ask('Pick a file'))})]),
             newComboBox(HIGHLIGHTERS)
               .withCaption('Highlighting')
               .bind('hl'),
             newOutputEditor({hl})
               .withCaption({lines})]);
  logHlAssoc(filename,hl);
end;
