worldHeight:=40;
worldWidth:=120;

initWorld->ord(random(worldHeight*worldWidth)<0.5);
printWorld(w:intList)->printf("%X%",copy(each(c,[' ','#']%w,&),worldWidth*[0..worldHeight-1],worldWidth));

memoized
shift(di,dj)->flatten(each(i,[0..worldHeight-1],
                      each(j,[0..worldWidth-1],((i+di+worldHeight) mod worldHeight)*worldWidth+
                                                (j+dj+worldWidth)  mod worldWidth )));

neighbors(w)->w%shift(-1,-1)+w%shift( 0,-1)+w%shift( 1,-1)+
              w%shift(-1, 0)+               w%shift( 1, 0)+
              w%shift(-1, 1)+w%shift( 0, 1)+w%shift( 1, 1);

TRANSITION:=[[0,0,0,1,0,0,0,0,0],
             [0,0,1,1,0,0,0,0,0]];

step(w)->step(w,TRANSITION%%neighbors(w));
step(w,t)->each(i,[0..size(w)-1],t%%i%(w%i));

play->play(initWorld);
play(w)->print("\n\n\n")|printWorld(w)|sleep(0.1)|play(step(w));

mutable sleepTime:=0.1;
mutable wantQuit:=false;

playUntilStatic(w)->begin
  local world:=w;
  while(not(wantQuit),begin
    clearPrint;
    printWorld(world);
    sleep(sleepTime);
    local newWorld:=step(world);
    world==newWorld ? wantQuit:=true : void;
    world:=newWorld;
    void;
  end);
end;

playSteps(w,n>0)->begin
  local world:=w;
  each(i,[1..n],begin
    printWorld(world);
    sleep(sleepTime);
    world:=step(world);
    void;
  end);
end;

main->(sleepTime:=0) | playSteps(initWorld,10);
main("-nowait",...)->(sleepTime:=0) | main@(tail($params));
main("-long")->pEach(task,[{playUntilStatic(initWorld)},{begin ask('Press Enter to quit.'); wantQuit:=true; end}],task());
main(steps)->playSteps(initWorld,softCast(steps));
