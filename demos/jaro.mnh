private setFlag(B:BooleanList,i:Int,newValue:Boolean)->0<=i<B.size ? B.each(b,index=i ? newValue : b) : fail;
private setFlag(B:BooleanList,i:Int)->setFlag(B,i,true);

//@SuppressUnusedWarning
jaroDistance("","")->1;
jaroDistance(A:String,B:String)->begin
  local matchDistance:=max(A.length,B.length) div 2-1;
                   local aChars:=A.chars;
  local bMatches:=(local bChars:=B.chars).each(c,false);
  local aMatches:=
  aChars.each(a,
    [max(0      ,index-matchDistance  )..
     min(bChars.size-1,index+matchDistance-1)].each(k,
       bChars[k]==a AND not(bMatches[k])
       ? begin
           bMatches:=bMatches.setFlag(k);
           true;
         end
       : false
    ,OR)
  );
  (local matchCount:=aMatches.trueCount)==0 ? 0 : begin
    local transpositions:=trueCount(aChars[aMatches]<>bChars[bMatches]);
    (matchCount/A.length+
     matchCount/B.length+
     (matchCount-transpositions/2)/matchCount)/3;
  end;
end;

private testJaro(A,B)->printf("The Jaro distance\v of\t%{$0.escape}s\t\n\vand\t%{$1.escape}s\tis\t%{jaroDistance($0,$1)}",A,B);

//@demo_for=strings, local variables/mutation
//*See http://rosettacode.org/wiki/Jaro_distance
main->testJaro(   "MARTHA",      "MARHTA") orElse
      testJaro(    "DIXON",    "DICKSONX") orElse
      testJaro("JELLYFISH",  "SMELLYFISH");
