USE inputs;

private memoized words->dict.fileLines.trim;

private relativeFrequency(L:List)->begin
  local Freq:=L.elementFrequency.sort;
  local f:=1/Freq.getInner(1).max;
  Freq.each(t,[t[1]*f,t[0]]);
end;

private memoized lengthTable->length(words).relativeFrequency;
private memoized firstTable->words.copy(0,1).relativeFrequency;
private memoized followerTable->words.pEach(word,word.length<2 ? void : word.copy([0..length(word)-2],2))
                                     .flatten
                                     .pEach(s,s.chars)
                                     .group(0)
                                     .pEach(g,g[0]=>g[1].getInner(1).relativeFrequency)
                                     .toMap;

private randomFromTable(T:List)->begin
  local index:=intRandom(T.size);
  while(T[index,0]<random,index:=intRandom(T.size));
  T[index,1];
end;

randomWord->begin
  local len:=lengthTable.randomFromTable;
  local result:=firstTable.randomFromTable;
  local lastChar:=result;
  [2..len].each(i,begin
    local nextChar:=followerTable[lastChar].randomFromTable;
    result&=nextChar;
    lastChar:=nextChar;
  end);
  result;
end;

@demo_for=http, memoization, map operations, strings
main(count)->begin
  local cnt:=toInt(count);
  assert(cnt>0,'count must be >0');
  [1..cnt].each(w,randomWord.print);
end;
