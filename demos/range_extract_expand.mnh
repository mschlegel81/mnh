private exampleList:=
  [0,  1,  2,  4,  6,  7,  8, 11, 12, 14,
  15, 16, 17, 18, 19, 20, 21, 22, 23, 24,
  25, 27, 28, 29, 30, 31, 32, 33, 35, 36,
  37, 38, 39];
private exampleRange:="-6,-3--1,3-5,7-11,14,15,17-20";

private extractRange(L:intList)->begin
  local deltaIdx:=-1|[0..size(L)-2].get(leading(L)+1<>tail(L))|(size(L)-1);
  [0..size(deltaIdx)-2].each(k,[L.get(deltaIdx.get(k)+1),L.get(deltaIdx.get(k+1))]);
end;

private mnhForm   (R)->"["&R.each(run,{$x=$y ? $x : $x&".."&$y}@run).join(",")&"]";
private normalForm(R)->    R.each(run,{$x=$y ? $x : $x&"-"& $y}@run).join(",")    ;

private range(x)->x;
private range(x0,x1)->[x0..x1];

private expandRange(s:string)->split(s,",").each(run,range@softCast(split(replace(run,["0".."9"]&"-",["0".."9"]&";"),";")),|);


//*See <a href="http://rosettacode.org/wiki/Range_expansion">http://rosettacode.org/wiki/Range_expansion</a>
//*and <a href="http://rosettacode.org/wiki/Range_expansion">http://rosettacode.org/wiki/Range_expansion</a>
main->print("Canonical form (as given by rosettacode.org):")|
      print("  The list\t"               ,                                    exampleList,
            "\n  can be represented as\t",            normalForm(extractRange(exampleList)),
            "\n  and be re-expanded to\t",expandRange(normalForm(extractRange(exampleList)))) |
      print("\n  The range\t"              ,                                    exampleRange,
            "\n  can be expanded to\t"     ,                        expandRange(exampleRange),
            "\n  and be re-compressed to\t",normalForm(extractRange(expandRange(exampleRange))) )|
      print("\nMNH form:")|
      print("  The list\t"               ,                                  exampleList,
            "\n  can be represented as\t",             mnhForm(extractRange(exampleList)),
            "\n  and be re-expanded to\t",toExpression(mnhForm(extractRange(exampleList)))());
