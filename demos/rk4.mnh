//*Solves the autonomous Differential equation y(0)=y0; dy/dt=F; returns y(dt)
RK4(F:expression,y0,dt>0)->begin
  local F1:=dt*F(y0       );
  local F2:=dt*F(y0+0.5*F1);
  local F3:=dt*F(y0+0.5*F2);
  local F4:=dt*F(y0    +F3);
  y0+(1/6)*(F1+2*F2+2*F3+F4);
end;

//*Solves the autonomous Differential equation y(0)=y0; dy/dt=F; returns y(tEnd)
rk4evolve(F,y0,dt,tEnd>0)->(y0:scalar ? y0 : [y0]) |rk4evolve(F,RK4(F,y0,dt),dt,round(tEnd-dt,3));
rk4evolve(F,y0,dt,tEnd  )->(y0:scalar ? y0 : [y0]);

private df     :={[1,$x%0*sqrt($x%1)]};
private initial:=[0,1];
private exact  :={1/16*($t^2+4)^2};

//*Solves the Runge-Kutta task from <fga href="http://rosettacode.org/wiki/Runge-Kutta_method">Rosetta Code</a>
main->begin
  local steps:=each(p,rk4evolve(df,initial,0.1,10),[p%0,p%1,abs(p%1-exact(p%0))]);
  printf("%4.1f\t%g\t%e",
         'time'|steps%%0,
         'approximation'|steps%%1,
         'error'|steps%%2);
end;  
