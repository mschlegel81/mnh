private unpack(s:String)->toExpression(replace(s,["{" ,","   ,"}"," "],
                                                 ["['","','","']",","]))();
private pack(L)->replace("#"&toString(L)&"#",["'",'"',"[","]","#{","}#"],
                                             ['' ,'' ,"{","}" ,"[","]" ]);
private consolidate(s:String)->unpack(s).consolidate.pack;
private consolidate(ListOfSets<>[])->
  size(ListOfSets)=1
  ? ListOfSets
  : consolidate(ListOfSets,flatten(ListOfSets).unique.sort);

private consolidate(ListOfSets,consolidationPoints)->
  consolidationPoints==[]
  ? ListOfSets
  : begin
      local mask:=ListOfSets.each(set,consolidationPoints.head in set);
      consolidate([ListOfSets.get(mask).flatten.unique.sort]|ListOfSets.get(not(mask)),consolidationPoints.tail);
    end;

private examples:=["[{A,B} {C,D}]","[{A,B} {B,C}]","[{A,B} {C,D} {D,B}]","[{h,i,k} {a,b} {c,d} {d,b} {f,g,h}]"];

@demo_for=math/graphs
//*See http://rosettacode.org/wiki/Set_consolidation
main->examples.each(e,print('Consolidation of ',e,"\n",
                            '              is ',consolidate(e)));
