private deg45:=arctan(1);

@demo_for=fractal, plotting
sc(depth)->begin
  local result:=[];
  local position:=[0.353553390593275,0.707106781186549]*2;
  local direction:=deg45;
  local half:={$level <=0 ? result|=[position+=[cos(direction),sin(direction)]] : begin
    half($level-1);
    direction+=deg45;
    result|=[position+=[cos(direction),sin(direction)]*sqrt(2)];
    direction+=deg45;
    half($level-1);

    direction-=deg45*2;
    result|=[position+=[cos(direction),sin(direction)]];
    direction-=deg45*2;

    half($level-1);
    direction+=deg45;
    result|=[position+=[cos(direction),sin(direction)]*sqrt(2)];
    direction+=deg45;
    half($level-1);
  end};
  [0..1].each(i,begin
    half(depth);
    direction-=deg45*2;
    result|=[position+=[cos(direction),sin(direction)]];
    direction-=deg45*2;
    void; end);
  (result|result.get([0]))/2^depth;
end;

main->begin
  plot.setOptions([['axisStyleX',0],['axisStyleY',0]]);
  plot   (sc(1),'l 32 GREY0.96875');
  addPlot(sc(2),'l 16 GREY0.9375');
  addPlot(sc(3),'l  8 GREY0.875');
  addPlot(sc(4),'l  4 GREY0.75');
  addPlot(sc(5),'l  2 GREY0.5');
  addPlot(sc(6),'l  1 black');
  plot.renderToFile(replace(myPath&"#","mnh#","png"),768,768,3);
end;
