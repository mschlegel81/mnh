private drop(list:IntCollection,toDrop:Int          )->list.minus([toDrop]);
private drop(list:IntCollection,toDrop:IntCollection)->list.minus( toDrop );

//*The cells that are connected
private
COMPONENTS:=[0..8].each(i,i+[0..8]*9)| //same row
            [0..8].each(i,i*9+[0..8])| //same column
            [0..8].each(i,[0..80].get(i=([0..80] div 27)*3+[0..80] mod 9 div 3))
                  .each(component,component.toSet); //same block
//*Returns the unique indexes of all cells connected to i (excluding i)
private memoized connectedIdx(i:Int)->COMPONENTS.each(c,i in c ? drop(c,i) : void).flatten.sort;
//*Returns the components
private memoized components(i)->COMPONENTS.each(c,i in c ? drop(c,i) : void);

//*A board is solved when all components contain numbers [1..9]
private isSolved(Board)->COMPONENTS.each(c,Board[c].sort==[1..9],AND);
//*A board is valid when all components may contain all numbers [1..9]
private isValid(Board)->Board.each(cell,!(cell==[]),AND) AND
                        COMPONENTS.each(c,Board[c].flatten.unique.sort==[1..9],AND);
//*A board's difficulty is given by the number of cells to fill
private difficulty(Board)->Board.each(cell,cell.isInt ? 0 : 1,+);

private
initialBoard->begin
  values:=1+random(9).sortPerm;
  indexes:=[0..8]*9+random(9).sortPerm;
  [0..80].each(i,i in indexes ? values[indexes=i,0] : [1..9]);
end;

BOARD_FORMAT_STRING:=
//!~
╔═══╤═══╤═══╦═══╤═══╤═══╦═══╤═══╤═══╗
║ x │ x │ x ║ x │ x │ x ║ x │ x │ x ║
╟───┼───┼───╫───┼───┼───╫───┼───┼───╢
║ x │ x │ x ║ x │ x │ x ║ x │ x │ x ║
╟───┼───┼───╫───┼───┼───╫───┼───┼───╢
║ x │ x │ x ║ x │ x │ x ║ x │ x │ x ║
╠═══╪═══╪═══╬═══╪═══╪═══╬═══╪═══╪═══╣
║ x │ x │ x ║ x │ x │ x ║ x │ x │ x ║
╟───┼───┼───╫───┼───┼───╫───┼───┼───╢
║ x │ x │ x ║ x │ x │ x ║ x │ x │ x ║
╟───┼───┼───╫───┼───┼───╫───┼───┼───╢
║ x │ x │ x ║ x │ x │ x ║ x │ x │ x ║
╠═══╪═══╪═══╬═══╪═══╪═══╬═══╪═══╪═══╣
║ x │ x │ x ║ x │ x │ x ║ x │ x │ x ║
╟───┼───┼───╫───┼───┼───╫───┼───┼───╢
║ x │ x │ x ║ x │ x │ x ║ x │ x │ x ║
╟───┼───┼───╫───┼───┼───╫───┼───┼───╢
║ x │ x │ x ║ x │ x │ x ║ x │ x │ x ║
╚═══╧═══╧═══╩═══╧═══╧═══╩═══╧═══╧═══╝~.replace('x','%s');

formatBoard(B)->BOARD_FORMAT_STRING.format@(B.map((x)->x.isInt ? x : ' '));
printBoard(B)->formatBoard(B).print;

private
solve(B)->begin
  needRun:=isValid(B);
  Board:=B;
  while needRun do
  begin
    needRun:=false;
    //intsOnly are the fixed/solved cells
    intsOnly:=Board.pEach(c,c.isInt ? c : []);
    Board:=Board.each(cell,
      cell.isInt //is cell already fixed/solved ?
      ? cell
      : begin
          //first try to solve the cell by restricting values based on already fixed/solved cells
          newList:=drop(cell,intsOnly[connectedIdx(index)].flatten);
          size(newList)=1
          ? begin
              needRun:=true;
              newList.toList.head;
            end
          : components(index).each(COMP,begin
              //try to solve based on all cells (including unsolved)
              tmp:=cell.drop(Board[COMP].flatten);
              size(tmp)=1
              ? begin
                  needRun:=true;
                  tmp.toList.head;
                end
              : void;
            end,head) orElse newList;
        end);
  end;
  Board;
end;

private randomFromSet(s:IntCollection)->s.toList[intRandom(s.size)];

private restrictRandom(B)->begin
  listIndexes:=[0..80].each(i,B[i].isInt ? void : i);
  listIndexes==[] ? B : begin
    randomListIdx:=listIndexes[intRandom(size(listIndexes))];
    [0..80].each(i,i=randomListIdx ? B[i].randomFromSet : B[i]);
  end;
end;

randomSolution->begin
  Board:=void;
  repeat
    Board:=initialBoard.restrictRandom.solve;
    while isValid(Board) AND not Board.isSolved
    do begin
      Board:=Board.restrictRandom;
      Board:=Board.solve;
    end;
  until Board.isSolved;
  Board;
end;

randomRiddle->begin
  riddle:=randomSolution;
  random(81).sortPerm.each(undefIdx,begin
    previousRiddle:=riddle;
    riddle:=[0..80].each(i,i=undefIdx ? [1..9] : riddle[i]);
    riddle.solve.isSolved ? void : riddle:=previousRiddle;
    void;
  end);
  print('Generated a riddle with difficulty: ',riddle.difficulty);
  riddle;
end;

//*For regression test: create a random riddle and print it out
main->randomRiddle.printBoard;

@demo_for=puzzle
//*Create count riddles
main(count)->begin
  iCount:=count.toInt;
  assert(iCount>0,'count must be >0');
  [1..iCount].pMap(()->randomRiddle.formatBoard.split("\n"))
             .chunkMap(3,(chunk)->(chunk.size in [2,3]
                                   ? chunk.transpose.map((line)->line.join(" "))
                                   : chunk))
             .flatMap
             .toList
             .join("\n")
             .print;
end;
