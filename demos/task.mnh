//@demo_for=datastores
//*Format is [[name1,timeSpent1],[name2,timeSpent2],...]
private datastore allTasks:=[].toMap;
//*Format is [name,startedAt]
private datastore runningTask:=[];

private endTask->runningTask==[] ? void : //If there is no running task, there is nothing to do
begin
  local timeSpentNow   :=systime- runningTask[1];
  printf("Task %s finished after %{formatTime('hh:nn:ss',$1)}s; total time spent: %{formatTime('hh:nn:ss',$2)}s",
         runningTask[0],
         timeSpentNow,
         local timeSpentTotal:=timeSpentNow+(allTasks[runningTask[0]] orElse 0));
  allTasks<<[runningTask[0],timeSpentTotal];
  runningTask:=[];
end;

//*Ends the current task and lists all tasks
main()->begin
  endTask;
  print('All tasks:');
  printf("  %s\t%{$1.isString?$1:formatTime('hh:nn:ss',$1)}s",
    'Task'      | allTasks.getInner(0),
    'Time spent'| allTasks.getInner(1));
end;

//*Resumes a task
main('?')->size(allTasks)>0
           ? main(ask('Task to resume',allTasks.getInner(0)))
           : print('There are no tasks to resume.');

//*Resumes a task or starts a new one
main(taskName)->begin
  endTask;
  (allTasks.getInner(0)=taskName).agg(OR) orElse false
  ? begin
      print('Resmuming task ',taskName);
    end
  : begin
      allTasks<<[taskName,0];
      print('New task ',taskName);
    end;
  runningTask:=[taskName,systime];
end;
