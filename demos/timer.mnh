private SAND_CHARS:=['a'..'z','A'..'Z','0'..'9','-','+','%','ยง',"'",'"'];
private UPPER_HALF:=//!~
 ____________
|AumgcabehnvB|
|LCwoidfipxDM|
|WOEyqklrzFPX|
 \0YQGstHRZ1/
  \82SIJT39/
   \-4UV5+/
    \%67ยง/
     \'"/
      \/ <*>~;
private LOWER_HALF:=UPPER_HALF.split("\n").tail.reverseList.join("\n").replace(['/','\','~'],['~','/','\']);
private memoized printOnce(...)->print@...;
private sandClock(startTime,endTime)->begin
  local timeLeft:=endTime-systime;
  local progress:=round(SAND_CHARS.size*max(0,min(1,(systime-startTime)/(endTime-startTime))));
  local sandDown:=progress=0 ? [] : SAND_CHARS[[0..progress-1]];
  local sandUp:=SAND_CHARS.minus(sandDown);
  local allLines:=(UPPER_HALF.replace(sandDown,' ').replace(sandUp,'#').replace('*',formatTime('hh:mm:ss',endTime))&"\n"&
                   LOWER_HALF.replace(sandDown,'#').replace(sandUp,' ').replace('*',formatTime('hh:mm:ss',timeLeft))).split("\n");
  "\f\n"&(allLines.leading|allLines.trailing.replace(' ','_')).join("\n");
end;

tolerantParseTime(input:string)->
  try({parseTime('hh:nn:ss',input)},
      {try({parseTime('hh:nn',input)},
           false)});

readTimeOrInterval(questionPrefix:string)->begin
  local result:=false;
  while(not(result.isNumeric),
    result:=ask(questionPrefix&' as HH:MM.SS or HH:MM').tolerantParseTime);
  result;
end;

readInterval->readTimeOrInterval('Enter interval') - parseTime('hh:nn:ss','00:00:00') + systime;
readTime    ->readTimeOrInterval('Enter time');
mutable endTime:=void;
mutable keypressed:=false;
mutable beepOnce:=false;

private timerLoop->begin
  async({begin
    ask('Press enter to cancel');
    keypressed:=true;
  end});

  local startTime:=systime;
  while(systime<endTime AND not(keypressed),begin
    sandClock(startTime,endTime).printOnce;
    sleep(0.1);
  end);
  sandClock(startTime,endTime).printOnce;
  beepOnce ? beep :
  while(not(keypressed),begin
    beep;
    sleep(1);
  end);
end;

main->begin
  endTime:=[['t',{readTime}],['i',{readInterval}],['a',{false}]][ask('Timer: [t]ime, [i]nterval or [a]bort?',['t','i','a'])]();
  endTime.isNumeric
  ? timerLoop
  : void;
end;
