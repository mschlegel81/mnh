private points:=each(p,each(i,[0..1199],sqrt(i)*[cos(2.39996323*i),sin(2.39996323*i)]),-25<p%0<25 and -25<p%1<25 ? p : void);

private norm(x)->sqrt(x%0*x%0+x%1*x%1);

private pathLength(points)->begin
  local result:=0;
  local lastPoint:=points%(size(points)-1);
  each(p,points,result:=result+norm(lastPoint-p),lastPoint:=p);
  result;
end;

private len4(a,b,c,d)->norm(b-a)+norm(c-b)+norm(d-c);

scSort(L)->begin
  local x:=L%%0; local xr:=[min(x),max(x)];
  local y:=L%%1; local yr:=[min(y),max(y)];
  local h:=max(xr%1-xr%0,yr%1-yr%0)/2;
  xr:=(xr%1+xr%0)*0.5;
  yr:=(yr%1+yr%0)*0.5;
  local x0:=xr-h;
  local x1:=xr+h;
  local y0:=yr-h;
  local y1:=yr+h;
  local mask:=sqr(x-x0)+sqr(y-y1)<
              sqr(x-x1)+sqr(y-y0);
  local result:=scSort_(x0,y0,x0,y1,x1,y1,x%    mask ,y%    mask )|
                scSort_(x1,y1,x1,y0,x0,y0,x%not(mask),y%not(mask));
  print('Path length after construction is ',round(pathLength(result),3));
  result;
end;

polish(L)->begin
  local result:=L;
  local changing:=true;
  while(changing,begin
    changing:=false;
    each(i,[0..size(result)-1],begin
      local beforeSwapping:=len4@(result%([i,i+1,i+2,i+3] mod size(result)));
      local afterSwapping :=len4@(result%([i,i+2,i+1,i+3] mod size(result)));
      afterSwapping<beforeSwapping ? begin
        changing:=true;
        local j0:=(i+1) mod size(result);
        local j1:=(i+2) mod size(result);
        result:=result%each(j,[0..size(result)-1],j=j0 ? j1 :
                                                  j=j1 ? j0 : j);
        void;
      end : void;
    end);
  end);
  print('Path length after polishing is    ',round(pathLength(result),3));
  result;
end;

private scSort_(ax:numeric,ay:numeric,bx:numeric,by:numeric,cx:numeric,cy:numeric,x:numericList,y:numericList)->
  size(x)>1
  ? (sqr(ax-cx)+sqr(ay-cy)>1E-12
    ? begin
        local mask:=sqr(x-ax)+sqr(y-ay) <
                    sqr(x-cx)+sqr(y-cy);
        scSort_(ax,ay,(ax+cx)/2,(ay+cy)/2,bx,by,x%    mask ,y%    mask ) |
        scSort_(bx,by,(ax+cx)/2,(ay+cy)/2,cx,cy,x%not(mask),y%not(mask));
      end
    : begin
        print('Aboring at ',ax,'  ',ay,' | ',
                            bx,'  ',by,' | ',
                            cx,'  ',cy,' with ',size(x),' points');
        each(i,[0..size(x)-1],[x%i,y%i]);
      end)
  : size(x)=1 ? [x|y] : [];

//*Approximately solves the Travelling Salesman Problem using sorting by Sierpinski-Curve
main->begin
  plot.setAxisStyle([0,0]);
  local L:=polish(scSort(points));
  plot(L|L%[0],'. l');
  plot.renderToFile(replace(myPath,'.mnh','.png'),768,768,4);
end;
