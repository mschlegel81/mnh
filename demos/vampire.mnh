private areFangs(fangs,sortedDigits)->fangs.digits.flatten.sort == sortedDigits;

private testRange(digitCount,mid)->testRange(10^(digitCount-1),mid,10^digitCount-1);
private testRange(t0,t1,t2)->abs(t2-t1)<abs(t1-t0) ? [t1..t2]
                                                   : [t0..t1];
private getFangs(i:Int)->begin
  local iDigits:=i.digits.sort;
  local toTest:=testRange(iDigits.size div 2,round(sqrt(i)));
  toTest:=toTest[i mod toTest=0];
  [i div toTest,toTest].transpose.pFilter({$f.areFangs(iDigits)});
end;

@SuppressUnusedWarning
isVampire(i:Int)->(i.digits.size and 1)=0 AND getFangs(i).size>=1;

private printVampire(i:Int)->begin
  local fangs:=getFangs(i);
  fangs==[]
  ? void
  : print(i," is a vampire. It's fangs are: ",join(fangs," or ")) orElse true;
end;

private verbosePrintVampire(i)->printVampire(i) orElse print(i,' is not a vampire.');

private vampireCandidateGenerator->{
begin
  local next:=9;
save;
  (next+=1) in [100,10000,1000000]
  ? next*=10
  : next;
end}.toIteratableExpression;

@demo_for=math/strings, generators
//*Solves the "Vampire-Number" task from http://rosettacode.org/wiki/Vampire_number
main('rosetta')->begin
  print('See http://rosettacode.org/wiki/Vampire_number');
  main;
  print('-----------------------------------------------');
  verbosePrintVampire(16758243290880);
  verbosePrintVampire(24959017348650);
  verbosePrintVampire(14593825548650);
end;

//*As above but abridged for regression test
main->vampireCandidateGenerator().lazyMap(::printVampire).head(25);
