
private prod(I:IntList)->I.agg(*);
private getFangs(i:Int)->begin
  iDigits:=i.digits.sort;
  dividers:=factorize(i).subSets.map(::prod);
  dividers:=dividers[10^(iDigits.size div 2-1)<=dividers and sqr(dividers)<=i];
  [dividers,i div dividers].transpose
    .filter({!($f mod 10=0).agg(AND) AND $f.digits.flatten.sort==iDigits})
    .sort;
end;

@SuppressUnusedWarning
isVampire(i:Int)->(i.digits.size and 1)=0 AND getFangs(i).size>=1;

private printableVampire(i:Int)->begin
  fangs:=getFangs(i);
  fangs==[]
  ? void
  : format("%s is a vampire. It's fangs are: %s",i, join(fangs," or "));
end;

private verbosePrintVampire(i)->begin
   v:=printableVampire(i);
   v.isVoid
   ? print(i,' is not a vampire.')
   : print(v);
end;

private vampireCandidateGenerator->{
begin
  next:=1259;
save;
  (next+=1) in [10000,1000000]
  ? next*=10
  : next;
end}.toIterableExpression;

@demo_for=math/strings, generators
//*Solves the "Vampire-Number" task from http://rosettacode.org/wiki/Vampire_number
main('rosetta')->begin
  print('See http://rosettacode.org/wiki/Vampire_number');
  main;
  print('-----------------------------------------------');
  verbosePrintVampire(16758243290880);
  verbosePrintVampire(24959017348650);
  verbosePrintVampire(14593825548650);
end;

//*As above but abridged for regression test
main->vampireCandidateGenerator().pMap(::printableVampire).head(25).map(::print);
