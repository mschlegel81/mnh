private target:="METHINKS IT IS LIKE A WEASEL";
private fitness(s:String)-> abs(ord(target.bytes)-
                                ord(     s.bytes)).agg(+);
private CHARSET:=['A'..'Z',' '];
private randomCharacter->CHARSET.get(intRandom(size(CHARSET)));

private multiplyAndSelect(parent:String,offspringCount, mutateProb)->
   ([parent]|map([1..offspringCount],{parent.bytes.each(c,random<mutateProb ? randomCharacter : c).join}))
   .pMap({[$i,fitness($i)]})
   .sort(1)
   .head.head;

private evolve(mutationFalloff,offspringCount,generationLimit)->begin
  local parent:=[0..length(target)-1].each(i,randomCharacter,&);
  local generationCount:=0;
  local mutationRate:=1;
  while(parent<>target AND generationCount<generationLimit,
  begin
    parent:=multiplyAndSelect(parent,offspringCount,mutationRate);
    mutationRate:=max(0.05,mutationRate*mutationFalloff);
    (generationCount+=1) mod 20 = 0
    ? printf('%4d %s (%3d)',generationCount,parent,fitness(parent))
    : void;
  end);
  printf('%4d %s',generationCount,parent);
end;

@demo_for=evolutionary algorithm
//*See http://rosettacode.org/wiki/Evolutionary_algorithm
main->evolve(0.9,20,10000);
