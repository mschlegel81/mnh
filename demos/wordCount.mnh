USE inputs;

private IGNORE_CHARS:=["","!","\"","#","&","'","(",")",",","-",".","/","0","1","2","3","4","5","6","7","8","9",":",";","=","?","[","]","^","_","`","$","%","*","+","<",">","@","\\","{","|","}","Ã"];
private cleanString(s:string)->s.replace(IGNORE_CHARS," ").replace("  "," ").trim.lower;
private cleanSplit(s:string)->s='' ? void : split(s,' ');

private fileWords(filename)->pEach(l,fileLines(filename),cleanSplit(cleanString(l))).flatten;

private analyze(wordList)->begin
  local total:=size(wordList);
  print('Words total:    ',total);
  local hist:=elementFrequency(wordList);
  print('Distinct words: ',size(hist));
  local top50:=head(hist.sort({$x%0>=$y%0}),50);
  print('Most frequent:');
  printf("%X%\t%X%\t%X%",
    'word' |(top50%%1),
    'count'|(top50%%0),
    '%'    |round(top50%%0/total*100,2));
end;

private filesWords(pattern)->each(f,files(pattern),|,fileWords(f));

//*Displays a short info on words in kjv.txt
main->analyze(fileWords(inputs.kjv));
//*Displays a short info on words in the given file or files matching a search pattern
main(fileOrPattern:string)->analyze(filesWords(fileOrPattern));
