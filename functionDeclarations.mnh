firstString(c:string)->begin
  local indexes:=indexOf(c.chars="'")+1;
  indexes.size<2
  ? ''
  : c.copy(indexes[0],indexes[1]-indexes[0]-1);
end;

namespacePart(c:string)->begin
  local tok:=c.tokenSplit;
  local idx:=indexOf(tok.matches('NAMESPACE'));
  tok[idx[0] orElse -1] orElse '?';
end;

obtainData->allFolders('.')
  .each(f,files(f&['/*.pas','/*.inc']),|)
  .each(file,
    file
    .fileLines
    .each(line,line.matches('registerRule')
               ? [file,index,line.firstString,line.namespacePart]
               : void),|)
  .sort(2).sort(3);

main->obtainData
  .each(line,line.join("\t"))
  .join("\n")
  .print;

main('GUI')->obtainData.showTable;

main(pattern)->
  print('Search pattern: ',escape(pattern)) orElse
  obtainData
  .each(line,line.join("\t"))
  .each(line,line.matches(pattern) ? line : void)
  .join("\n")
  .print;
