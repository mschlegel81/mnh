USE hash,testLog,mnhInfo;

tprint(...)->print@(formatTime('hh:nn.ss ',systime)|...);

private delp->(isLinux ? 'delp' : '..\lazarus64\fpc\3.0.0\bin\x86_64-win64\delp.exe').execPipeless(
       ['gui\lib\i386-win32',
        'gui\lib\x86_64-linux',
        'gui\lib\x86_64-win64',
   'consoles\lib\i386-win32',
   'consoles\lib\x86_64-linux',
   'consoles\lib\x86_64-win64']);

private templateToInc(inFile,outFile<>inFile)->begin
  local inLines:=inFile.fileLines;
  local pasId:=clean(inFile.splitFileName['filename'],['a'..'z','A'..'Z'],'_');
  local outText:=
  'CONST '&pasId&': array[0..'&(inLines.size-1)&"] of string=(\n"&
  inLines.each(line,line='' ? "''" : join('#'&(line.chars.ord))).join(",\n")&');';
  outFile.fileContents==outText
  ? void
  : writeFile(outFile,outText);
end;

private nameAndContents(filename)->'['&escape(filename)&','&escape(fileContents(filename))&']';

private ensurePackages->fileExists('resources/ensurePackages.mnh') ? void : begin
  writeFileLines('resources/ensurePackages.mnh','{'|
    (files('demos/*.mnh')|'demos/inputs/inputs.mnh'|files('packages/*.mnh'))
    .each(file,(index=0 ? '[' : ',') & nameAndContents(file))|
    "].each(f,matches(f[0],'packages/').not AND fileExists($0&f[0]) ? void : writeFile($0&f[0],f[1]))}","\n");
end;

private mutable currentCodeHash:=void;
private synchronized updateCurrentCodeHash->
currentCodeHash:=begin
  isLinux ? tprint('Checkstyle skipped!') : executor.execPipeless(['../checkstyle.mnh','fix']);
  ensurePackages;
  //convert here to ensure these files are considered in code hash calculation
  templateToInc('resources/examples.txt','core/res_examples.inc');
  templateToInc('resources/html_template.txt','core/res_html_template.inc');
  templateToInc('resources/ensureNotepad++Highlighting.mnh','core/res_ensureNppHighlighting.inc');
  templateToInc('resources/ensureAssoc.mnh'     ,'core/res_ensureAssoc.inc');
  templateToInc('resources/ensureAssoc_imig.mnh','core/res_ensureAssoc_imig.inc');
  templateToInc('resources/removeAssoc.mnh'     ,'core/res_removeAssoc.inc');
  templateToInc('resources/removeAssoc_imig.mnh','core/res_removeAssoc_imig.inc');
  templateToInc('resources/ensurePackages.mnh','core/res_ensurePackages.inc');
  local relevantFiles:=allFiles('.','*.lpr;*.pas;*.inc').each(f,splitFileName(f)['filename']='code_hash.inc' ? void : f);
  //update code hash
  local result:=fileStats(relevantFiles).getInner(3).agg(+).hex;
  fileContents('core/code_hash.inc')=="writeln(':"&result&"');"
  ? void
  : begin
      writeFile('core/code_hash.inc',"writeln(':"&result&"');");
      tprint('core/code_hash.inc updated with current hash ',result);
    end;
  result;
end;

binName(FI:int)->format('%smnh%s%s%s',
                                (FI and 1)>0 OR isLinux ? './' : 'bin32/',
                                (FI and 2)>0 ? '' : '_light',
                                (FI and 4)>0 ? '_debug' : '',
                                isLinux ? '' : '.exe');

buildableVersions:=isLinux ? [0,2,4,6] : [0..7];

private exeHasWrongVersion(FI:int)->begin
  local exeName:=binName(FI);
  fileExists(exeName)
  ? exeName.codeVersion != (currentCodeHash orElse updateCurrentCodeHash)
  : true;
end;

private deleteStubbornly(file:string)->begin
  local tries:=3;
  while(fileExists(file) AND tries>0,
    deleteFile(file) ? void : begin
    tries-=1;
    sleep(0.1);
  end);
end;

private myMove(source,dest:string)->fileExists(source) ? begin
  deleteStubbornly(dest);
  moveFile(source,dest) ? void : tprint('File move ',source,' -> ',dest,' failed!');
end : tprint('File  ',source,' does not exist');

private BUILD_LOG_NAME:='build.log';

private build(FI:int)->
exeHasWrongVersion(FI) ? begin
  local bin64:=(FI and 1)>0;
  local full :=(FI and 2)>0;
  local debug:=(FI and 4)>0;
  local compiler:=isLinux ? 'lazbuild'
                          : bin64 ? '..\lazarus64\lazbuild.exe'
                                  : '..\lazarus32\lazbuild.exe';
  local exeName:=binName(FI);
  local outName:=(full ? 'gui/mnh' : 'consoles/mnh_light') & (debug ? '_debug' : '') & (isLinux ? '' : '.exe');
  local parameters:=['-B','--bm='&(debug ? 'Default' : 'deployment'),full ? 'gui/mnh_gui.lpi' : 'consoles/mnh_light.lpi'];

  deleteStubbornly(exeName);
  tprint('Compiling ',exeName,' logging to ',BUILD_LOG_NAME);
  appendFileLines(BUILD_LOG_NAME,[repeat('*',80),formatTime('dd.mm.yyyy hh:nn.ss ',systime),compiler&' '&(parameters.join(' ')),repeat('*',80)]|exec(compiler,parameters));
  myMove(outName,exeName);
  not(exeHasWrongVersion(FI));
end : tprint('Build of ',binName(FI),' skipped ') orElse true;

private call7z(fileList,suffix)->isLinux ? void : begin
  local zipParams:=['a','-mx=9',format('versions/mnh5_%s_%s.7z',formatTime('yyyymmdd',systime),suffix)]|fileList;
  execPipeless("c:/Program Files/7-Zip/7z.exe",zipParams);
  execPipeless("c:/Program Files (x86)/7-Zip/7z.exe",zipParams);
end;

private movePairs:=[['mnh.exe','mnh64.exe'],
                    ['mnh_light.exe','mnh64_light.exe'],
                    ['bin32/mnh.exe','mnh.exe'],
                    ['bin32/mnh_light.exe','mnh_light.exe']];
private verboseMove(src,dest)->begin
  printf('move %-19s -> %-19s',src,dest);
  moveFile(src,dest);
end;

//*Make distro 7z - including build if needed
main('distro')->isLinux ? fail('Distro building is currently not supported under Linux') : begin
  updateCurrentCodeHash;
  [0..3].each(i,
  begin
    local buildOk:=build(i);
    local testOk:=testAlreadyPassed(binName(i));
    buildOk ? void : tprint('Building of ',binName(i),' failed');
    testOk ? void : tprint(binName(i),' has not been tested successfully');
    buildOk and testOk;
  end,
  AND)
  ? begin
      execPipeless('..\upx.exe',['-9','mnh_light.exe']);
      call7z(['mnh.exe','mnh_light.exe'],'64bit');
      movePairs.each(pair,verboseMove@pair,AND)
      ? begin
          execPipeless('..\upx.exe',['-9','mnh_light.exe']);
          call7z(['mnh.exe','mnh_light.exe'],'32bit');
          movePairs.get([3..0]).each(pair,verboseMove@(pair.get([1,0])));
        end
      : tprint('Error moving files! 32bit distro has not been created!');
      //Linux executables exist and are less than one hour old
      ['mnh','mnh_light'].each(file,fileExists(file) AND systime-file.fileInfo["time"]<1/24,AND)
      AND linuxTestsPassed
      ? begin
          execPipeless('..\upx.exe',['-9','mnh_light']);
          call7z(['mnh','mnh_light'],'64bit_Linux');
        end
      : void;
    end
  : void;
  fileExists('distro_hook.mnh') ? executor.execPipeless(['distro_hook.mnh']) : void;
end;

//*Build - ignoring potentially existing executables
main('build')->begin
  updateCurrentCodeHash;
  buildableVersions.each(i,build(i),AND);
end;

//*Build - ignoring potentially existing executables
main->main('build');

//*Deletes all nppBackup folders, temporary object files, executables and png-files
main('clean')->begin
  allFolders('..')
  .each(f,splitFileName(f)['filename'] in ["nppBackup","backup"] ? f : void)
  .each(f,begin
           tprint('Deleting ',f);
           deleteDir(f);
          end);
  (files(allFolders('.')&'/*.png')|
   files(allFolders('..')&'/*.bak')|
   files('resources/ensureDemos.mnh')|
   files('resources/ensurePackages.mnh')|
   files('core/res_*.inc')|
   allFiles('versions','*.exe')|
   allFiles('gui','*.exe')|
   allFiles('consoles','*.exe')|
   files('make*.log.txt'))
  .unique
  .each(file,tprint('Deleting ',file,' ',deleteFile(file) ? 'ok' : 'ERR!'));
  delp;
end;

private testMnh(...)->begin
  updateCurrentCodeHash;
  buildableVersions
  .each(i,
  i in buildableVersions.minus([4..7])
  ? build(i)
  : void,
  exeHasWrongVersion(i)
  ? void
  : testAlreadyPassed(binName(i))
    ? tprint('Test of  ',binName(i),' skipped')
    : tprint('Testing  ',binName(i)) orElse
      binName(i).execPipeless(['-el5','-quiet','regTest/regTest.mnh','resources/examples.txt'.splitFileName['expanded']]|...) and testAlreadyPassed(binName(i))
      ? void
      : tprint('Test failed') orElse false,
  AND);
end;

//*Tests all files
main('test')->testMnh;

//*Retests all files
main('retest')->testMnh('retest');

//*Only prepare code hash
main('prepare')->updateCurrentCodeHash;

private memoized acceptedMainParameters->inspect['declares','main','subrules'].getInner('pattern').clean(['a'..'z','A'..'Z'],' ').trim.each(p,p='' ? void : p);
private isSameFileName(i:int,s:string)->begin
  local case:=isLinux ? {$x} : {$x.upper};
  (binName(i).splitFileName['expanded'].case)==
  (s         .splitFileName['expanded'].case);
end;
private isSimilarFile(i:int,s:string)->pos(s.upper,binName(i).upper)>=0;

private buildAndTest(version:int)->begin
  local versionToTest:=buildableVersions[intRandom(buildableVersions.size)];
  local buildOk:=build(versionToTest);
  local skip:=false;
  buildOk
  ? begin
      skip:=testAlreadyPassed(binName(versionToTest));
      skip
      ? tprint('Skipping already passed test')
      : begin
          tprint('Testing ',binName(versionToTest));
          binName(versionToTest).exec(['-el5','-quiet','regTest/regTest.mnh','resources/examples.txt'.splitFileName['expanded']]|...);
          testAlreadyPassed(binName(versionToTest))
          ? void
          : tprint('  TEST FAILED!') orElse beep;
        end;
    end
  : tprint('  BUILD FAILED!') orElse beep;
  skip;
end;

//*Continuous integration run - automatically build and test every 5 minutes
main('continuous')->begin
  local skipCount:=0;
  while(skipCount<=4,begin
    tprint("\n\nContinuous run - building/testing");
    updateCurrentCodeHash;
    buildAndTest(intRandom(buildableVersions.size))
    ? skipCount+=1
    : tprint('Continuous run - sleeping') orElse sleep(5*60);
  end);
  buildableVersions.each(ver,buildAndTest(ver));
end;

//*Several steps in succession
main(...)->begin
  updateCurrentCodeHash;
  ... .each(p,
  buildableVersions
  .each(i,isSameFileName(i,p) ? build(i) : false,OR)
  ? void
  : buildableVersions
    .each(i,isSimilarFile(i,p) ? build(i) : false,or)
    ? void
    : p in acceptedMainParameters
      ? main(p)
      : fail("Don't know what to do with: "+p));
end;
