//*Returns a list suitbable for input of rule folderChanged
timedFolderContent(folderPath:String)->fileInfo(folderPath&'/*').each(info,[info['path'],info]).sort(0);
//*Returns the updated folder content or false if the content is unchanged
@SuppressUnusedWarning=all
changedFolderContent(folderPath:String,previousContent:List)->begin
  local currentContent:=timedFolderContent(folderPath);
  currentContent==previousContent
  ? false
  : currentContent;
end;

private changeToContent(L:List)->L.each(el,el[1]='deleted' ? void : el[[0,2]]).sort(0);
private
waitForFolderChange(folderPath:String,maxSleepTimeInSeconds:Numeric,timeoutInSeconds:Numeric,initialContent)->begin
  local scanInterval:=-1;
  local currentContent:=initialContent;
  local timeout:=time+timeoutInSeconds;
  while(currentContent==initialContent AND time<timeout,begin
    sleep(scanInterval:=min(scanInterval+1,maxSleepTimeInSeconds));
    currentContent:=timedFolderContent(folderPath);
  end);
  local names:=initialContent.getInner(0).union(currentContent.getInner(0)).sort;
  names.each(n,n in initialContent.getInner(0)
               ? n in currentContent.getInner(0)
                 ? initialContent[n]==currentContent[n]
                   ? [n,'',currentContent[n]]
                   : [n,'modified',currentContent[n]]
                 : [n,'deleted',initialContent[n]]
               : [n,'new',currentContent[n]]);
end;

changeSymbol:=[['',''],['modified','*'],['deleted','-'],['new','+']];


//*Waits for a folder change and returns the changes as a list:
//*[[filename,changetype,fileInfo],...]
//*where changetype is one of ['new','deleted','modified',''].
//*If the file is deleted, fileInfo is the info of the old file
waitForFolderChange(folderPath:String,maxSleepTimeInSeconds:Numeric,timeoutInSeconds:Numeric)->waitForFolderChange(folderPath,maxSleepTimeInSeconds,timeoutInSeconds,timedFolderContent(folderPath));
//*As above but without timeout
waitForFolderChange(folderPath:String,maxSleepTimeInSeconds:Numeric)->waitForFolderChange(folderPath,maxSleepTimeInSeconds,2^31);

//*Returns a list as waitForFolderChange but without waiting
pseudoFolderChange(folderPath:String)->timedFolderContent(folderPath).each(e,[e[0],'',e[1]]);

//*Waits for a folder change and calls onChange when the folder is changed
watchFolder(folderPath:String,onChange:Expression(1),maxSleepTimeInSeconds:Numeric)->
  async({begin
    local contents:=timedFolderContent(folderPath);
    while(true,begin
      contents:=waitForFolderChange(folderPath,maxSleepTimeInSeconds,2^31,contents);
      onChange(contents);
      contents:=contents.changeToContent;
    end);
  end});

//*Returns a temp file name by the specified pattern.
//*pattern must contain exactly one "#" as placeholder
tempFile(pattern:String)->
  trueCount(pattern.chars='#')!=1
  ? fail('pattern must contain exactly one "#" as placeholder')
  : begin
      local tmpFileIndex:=0;
      while(fileExists(replace(pattern,'#',tmpFileIndex.toString)),tmpFileIndex+=1);
      replace(pattern,'#',tmpFileIndex.toString);
    end;

//*Returns the file time of the newest file in the subTree or -1 if there is no file
//*Optional parameters indicate patterns
maxFileTime(folder,...)->allFiles(folder,...==[] ? void : ...).fileInfo.getInner('time').agg(max) orElse -1;

//*Returns the file time of the given file
fileTime(file)->file.fileInfo['time'] orElse -1;

isLinux:=mnhInfo['targetOs']='Linux' orElse false;

//*Returns true if the file names indicate equal files
isSameFileName(filename1:String,filename2:String)->begin
  local case:=isLinux ? {$x} : {$x.upper};
  (filename1.expandedFileName.case)==
  (filename2.expandedFileName.case);
end;
