//*Wraps sourceGenerator so that it generates (at max) for the given period of time
@SuppressUnusedWarning=all
timingOutGenerator(sourceGenerator:IteratableExpression,runForSeconds:Numeric)->
{begin
   local gen:=sourceGenerator;
   local timeout:=time+runForSeconds;
 save;
   time<timeout
   ? gen()
   : void;
 end}.toGenerator;

//*Returns a list of all integer pairs [a,b] with initialSum<=(a+b)<=finalSum and a,b>=minElementValue, ordered by (a+b)
pairGenerator(initialSum,finalSum,minElementValue<=initialSum)->{begin
  local k:=initialSum;
  local i:=minElementValue-1;
save;
  (k-i<=minElementValue) ? begin k+=1; i:=minElementValue-1; end : void;
  k<=finalSum ? [i+=1,k-i] : void;
end}.toGenerator;

//*Returns a list of all integer pairs [a,b] with initialSum<=(a+b) and a,b>=1, ordered by (a+b)
//*Warning: (potentially) infinite generator!
pairGenerator(initialSum)->pairGenerator(initialSum,2^63-1,1);

//*Returns an iterator returning all elements of the input list
listIterator(l:List)->{begin
  local index:=rangeGenerator(0,l.size);
  local value:=l;
save; value[index() orElse -1]; end}.toGenerator;

//*Returns a chunk iterator
chunkIterator(iter:Iteratable,chunkSize>=1)->{begin
  local g:=iter.isIteratableExpression
           ? iter
           : listIterator(iter.toList);
  local next:={begin
    local r:=g.head(chunkSize);
    r==[] ? void : r;
  end};
save;
  next();
end}.toGenerator;
