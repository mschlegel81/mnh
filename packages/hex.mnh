private HEX_DIGITS:=['0'..'9','A'..'F'];
private HEX_MAP:=HEX_DIGITS.each(i,[i,index]).toMap;
//@SuppressUnusedWarning
//*Converts i to hexadecimal representation with an even number of digits
intToHex(i:Int)->begin
  local h:=format('%x',i);
  (h.length and 1)=1
  ? '0'&h
  :     h;
end;

private hexToDigits(h:String)->HEX_MAP.getAll(h.upper.clean(HEX_DIGITS,'').chars);

//@SuppressUnusedWarning
//*Converts hexadecimal string h to bytes
hexToBytes(h:String)->begin
  local d:=h.hexToDigits.reverseList;
  (d.size and 1)=1 ? d|=0 : void;
  local i:=[0..d.size div 2-1]*2;
  d[i]+d[i+1]*16;
end;

//@SuppressUnusedWarning
//*Converts hexadecimal string h to an integer if in range or to a real otherwise
hexToNumeric(h:String)->h.hexToDigits.composeDigits(16);

//@SuppressUnusedWarning
hexPlus(a:String,b:String)->begin
  local A:=hexToDigits(a).reverseList;
  local B:=hexToDigits(b).reverseList;
  while(A.size<B.size,A|=0);
  while(B.size<A.size,B|=0);
  local carry:=0;
  local C:=(A+B).each(x,begin
    carry:=(local c:=x+carry) div 16;
    c and 15;
  end);
  carry>0          ? C|=carry : void;
  (C.size and 1)=1 ? C|=0     : void;
  HEX_DIGITS[C.reverseList].join;
end;
