private hexPoints(x0,x1,y0,y1,n)->
begin
  // kx*ky = n
  // kx/ky = (x1-x0)/(y1-y0)*sqrt(0.5)
  // kx    = ky   * (x1-x0)/(y1-y0)*sqrt(0.5)
  // kx    = n/kx * (x1-x0)/(y1-y0)*sqrt(0.5)
  // kx    = sqrt(n    * (x1-x0)/(y1-y0)*sqrt(0.5))
  local kx:=max(2,round(sqrt(n*(x1-x0)/(y1-y0)*sqrt(0.5))));
  local ky:=max(2,n div (kx*2)*2+1);
  local edges:=[];

  local outIdx:=0;
  local P:=[0..ky-1].cross([0..kx-1]).each(c,
  begin
    local ix:=c[1];
    local iy:=c[0];
    ix==(kx-1) AND (iy and 1)=1 ? void :
    begin
      ////Straight upward neighbor:
      ix in [0,kx-1] AND (iy and 1)=0 AND iy<ky-1
      ? edges||=[outIdx,outIdx+kx+kx-1]
      : void;
      ////Right neighbor:
      ix<kx-1-(iy and 1)
      ? edges||=[outIdx,outIdx+1]
      : void;
      //upper right neighbor
      ix<kx-1 AND iy<ky-1
      ? edges||=[outIdx,outIdx+kx]
      : void;
      //upper left neighbor:
      (1-(iy and 1))<=ix<kx AND iy<ky-1
      ? edges||=[outIdx,outIdx+kx-1]
      : void;
      outIdx+=1;
      [ix+0.5*(iy and 1),
       iy*sqrt(0.5)];
    end;
  end);

  local edgesByPoint:=edges.each(edge,[edge[0],edge],[edge[1],edge])
                           .group(0)
                           .each(g,[g[0,0],g.getInner(1)])
                           .toMap;
  local triangles:= edges.each(edge,intersect(edgesByPoint[edge[0]].flatten,
                                              edgesByPoint[edge[1]].flatten)
                                    .minus(edge)
                                    .each(k,sort(edge|k)),|)
                         .unique;
  local f:=[(x1-x0)/P.getInner(0).max,
            (y1-y0)/P.getInner(1).max];
  local o:=[x0,y0];
  P:=P.map({$p*f+o});
  [P,triangles];
end;

//*Returns a list of edges in the polyon (in node index space)
//*The returned edges are sorted
private edges(poly:IntList)->transpose([[0..poly.size-1],[1..poly.size-1,0]]).map({poly.getAll($i).sort});

private cutLine(F:Expression(2),x:NumericList(2),dx:NumericList(2),level:Numeric,
                t_:Numeric,F0:Numeric,F1:Numeric,quality:Int)->begin
  local t0:= 0; local f0:=F0         -level;
  local t1:= 1; local f1:=F1         -level;
  local tm:=t_; local fm:=F@(x+dx*t_)-level;
  [1..quality].each(step,begin
    f0*fm>0
    ? begin f0:=fm; t0:=tm; end
    : begin f1:=fm; t1:=tm; end;
    local tm_:=t1-f1*(t0-t1)/(f0-f1);
    tm:=0<=tm_<=1
        ? tm_
        : (t0+t1)/2;
    fm:=F@(x+dx*tm)-level;
  end);
  x+dx*tm;
end;

USE customFormUtil;

@SuppressUnusedWarning=all
contourPlot(F:Expression(2),x0,x1>x0,y0,y1>y0,triangleCount>=2,contourLevels:NumericList,qualityLevel>=0,interactive:Boolean)->
  plot() orElse addContourPlot(F,x0,x1,y0,y1,triangleCount,contourLevels,qualityLevel,interactive);
addContourPlot(F:Expression(2),x0,x1>x0,y0,y1>y0,triangleCount>=2,contourLevels:NumericList,qualityLevel>=0,interactive:Boolean)->begin
  local geometry:=hexPoints(x0,x1,y0,y1,triangleCount);
  local nodes    :=geometry[0].map({$p|(F@$p)});
  local triangles:=geometry[1];
  //Convert geometry to a list of adjacent edges
  local edgePairs:=[].toSet;
  local edgeList:=[].toSet;
  triangles.each(tri,begin
    local e:=tri.edges;
    e.subSets(2).each(pair,edgePairs|=[pair.sort]);
    e.each(edge,edgeList|=[edge]);
    void;
  end);
  edgeList:=edgeList.sort;
  local edgeZ0:=nodes.getAll(edgeList.getInner(0));
  local edgeZ1:=nodes.getAll(edgeList.getInner(1));
  local edgeXY0:=edgeZ0.getInner([0,1]); edgeZ0:=edgeZ0.getInner(2);
  local edgeXY1:=edgeZ1.getInner([0,1]); edgeZ1:=edgeZ1.getInner(2);
  edgeList:=edgeList.pEach(e,[e,index]).toMap;
  edgePairs:=edgePairs.pMap({edgeList.getAll($p)});

  edgeZ1 :=1/(edgeZ1 -edgeZ0 );
  edgeXY1:=  (edgeXY1-edgeXY0);

  local dataForLevel:={begin
    local t:=($level-edgeZ0)*edgeZ1;
    local mask:=0<=t<=1;
    local cut:=edgeXY0+edgeXY1*t;
    qualityLevel>0
    ? cut:=mask.pEach(m,m
         ? cutLine(F,edgeXY0[index], edgeXY1[index],$level,
                     t[index],edgeZ0[index],edgeZ0[index]+1/edgeZ1[index],qualityLevel)
         : 0)
    : void;
    edgePairs.pEach(p,mask[p[0]] AND mask[p[1]]
                      ? [cut[p[0]],cut[p[1]],[Nan,Nan]]
                      : void,|);
  end};

  contourLevels
  .sort
  .map(dataForLevel)
  .each(sample,
    sample.size==0
    ? void
    : sample.addPlot(contourLevels.size=0 ? 'black' : 'HUE'&(index*2/(3*contourLevels.size-1))));

  interactive ? void : return void;

  display;
  local x:=0;
  local y:=0;
  local f:=0;
  showDialog('Interactive Contour Plot',[
    newLabel.withCaption({'x='&x}),
    newLabel.withCaption({'y='&y}),
    newLabel.withCaption({'f(x,y)='&f}),
    newPlotLink.withMouseMoved({begin
      x:=$x;
      y:=$y;
      f:=F($x,$y);
      local lines:=dataForLevel(f);
      lines.size==0 ? void : addPlot(lines,'black');
      display;
      lines.size==0 ? void : removePlot;
    end}),
    newPlotDock]);
end;

densityPlot(F:Expression(2),x0,x1>x0,y0,y1>y0,sampleCount>=4,interactive:Boolean)->
  plot() orElse addDensityPlot(F,x0,x1,y0,y1,sampleCount,interactive);

addDensityPlot(F:Expression(2),x0,x1>x0,y0,y1>y0,sampleCount>=4,interactive:Boolean)->begin
  local geometry:=hexPoints(x0,x1,y0,y1,sampleCount);
  local nodes    :=geometry[0];
  local triangles:=geometry[1].pEach(t,
    [nodes[t],F@(nodes[t].agg(+)/nodes[t].size)]);

  local z0:=triangles.getInner(1).min;
  local z1:=triangles.getInner(1).max;
  z1:=1/(z1-z0);
  local c:={format('polygon fs 0 TI0 RGB%s,%s,%s',
  1-max(2*$x-1       ,0),
  1-max(1-2*$x,2*$x-1,0),
  1-max(1-2*$x       ,0))};
  local style:={c(($z-z0)*z1)};

  triangles
  .pMap({[$s[0],style($s[1])]})
  .each(polygon, addPlot@polygon);

  interactive ? void : return void;

  display;
  local x:=0;
  local y:=0;
  local f:=0;
  showDialog('Interactive Density Plot',[
    newLabel.withCaption({'x='&x}),
    newLabel.withCaption({'y='&y}),
    newLabel.withCaption({'f(x,y)='&f}),
    newPlotLink.withMouseMoved({begin
      x:=$x;
      y:=$y;
      f:=F($x,$y);
    end}),
    newPlotDock]);
end;

private PLOT_STYLE_KEYS:=['scale','color','ti','style','style2'];
type PlotStyle(m:Map)->m.getInner(0).minus(PLOT_STYLE_KEYS).size=0;
private modifyStyle(S:PlotStyle,key=='style2',value)->begin
  local R:=S;
  R[key]:=isVoid(S[key])
          ? value
          : S[key]&' '&value;
  R.toPlotStyle;
end;
private modifyStyle(S:PlotStyle,key in PLOT_STYLE_KEYS,value)->begin
  local R:=S;
  R[key]:=value;
  R.toPlotStyle;
end;

@SuppressUnusedWarning=all
@Override
toString(style:PlotStyle)->style.getInner(1).join(" ");

LINE     :=['style'=>'line'     ].toMap.toPlotStyle; LINE     (S:PlotStyle)->modifyStyle(S,'style','line'     );
STEPLEFT :=['style'=>'stepLeft' ].toMap.toPlotStyle; STEPLEFT (S:PlotStyle)->modifyStyle(S,'style','stepLeft' );
STEPRIGHT:=['style'=>'stepRight'].toMap.toPlotStyle; STEPRIGHT(S:PlotStyle)->modifyStyle(S,'style','stepRight');
BAR      :=['style'=>'bar'      ].toMap.toPlotStyle; BAR      (S:PlotStyle)->modifyStyle(S,'style','bar'      );
BOX      :=['style'=>'box'      ].toMap.toPlotStyle; BOX      (S:PlotStyle)->modifyStyle(S,'style','box'      );
ELLIPSE  :=['style'=>'ellipse'  ].toMap.toPlotStyle; ELLIPSE  (S:PlotStyle)->modifyStyle(S,'style','ellipse'  );
TUBE     :=['style'=>'tube'     ].toMap.toPlotStyle; TUBE     (S:PlotStyle)->modifyStyle(S,'style','tube'     );
POLYGON  :=['style'=>'polygon'  ].toMap.toPlotStyle; POLYGON  (S:PlotStyle)->modifyStyle(S,'style','polygon'  );
DOT      :=['style'=>'.'        ].toMap.toPlotStyle; DOT      (S:PlotStyle)->modifyStyle(S,'style','.'        );
PLUS     :=['style'=>'+'        ].toMap.toPlotStyle; PLUS     (S:PlotStyle)->modifyStyle(S,'style','+'        );
CROSS    :=['style'=>'x'        ].toMap.toPlotStyle; CROSS    (S:PlotStyle)->modifyStyle(S,'style','x'        );
IMPULSE  :=['style'=>'i'        ].toMap.toPlotStyle; IMPULSE  (S:PlotStyle)->modifyStyle(S,'style','i'        );
withDot    (S:PlotStyle)->modifyStyle(S,'style2','.');
withPlus   (S:PlotStyle)->modifyStyle(S,'style2','+');
withCross  (S:PlotStyle)->modifyStyle(S,'style2','x');
withImpulse(S:PlotStyle)->modifyStyle(S,'style2','i');
filled     (S:PlotStyle)->modifyStyle(S,'style2','f');
fillSolid  (S:PlotStyle)->modifyStyle(S,'style2','fs');
withScale(S:PlotStyle,scaleModifier>=0.0)->modifyStyle(S,'scale',scaleModifier);
coloredBlack (S:PlotStyle)->modifyStyle(S,'color','black');
coloredRed   (S:PlotStyle)->modifyStyle(S,'color','red');
coloredBlue  (S:PlotStyle)->modifyStyle(S,'color','blue');
coloredGreen (S:PlotStyle)->modifyStyle(S,'color','green');
coloredPurple(S:PlotStyle)->modifyStyle(S,'color','purple');
coloredOrange(S:PlotStyle)->modifyStyle(S,'color','orange');
coloredRGB   (S:PlotStyle,r:Numeric,g:Numeric,b:Numeric)->modifyStyle(S,'color',format('RGB%s,%s,%s',r,g,b));
coloredHSV   (S:PlotStyle,h:Numeric,s:Numeric,v:Numeric)->modifyStyle(S,'color',format('HSV%s,%s,%s',h,s,v));
coloredHUE   (S:PlotStyle,hue:Numeric)->modifyStyle(S,'color',format('HUE%s',hue));
coloredGREY  (S:PlotStyle,brightness:Numeric)->modifyStyle(S,'color',format('GREY%s',brightness));


setPlotAutoscaleFactor(factor) ->
  setOptions('autoscaleFactor',factor);
setPlotAutoscale(axis in ['x','y'],enableAutoscale:Boolean) ->
  setOptions('autoscale'&upper(axis),enableAutoscale);
setPlotAxisStyle(axis in ['x','y'],tics:Boolean,majorGrid:Boolean,minorGrid<=majorGrid) ->
  setOptions('axisStyle'&upper(axis),(tics ? 1 : 0) or (majorGrid ? 2 : 0 ) or (minorGrid ? 4: 0));
setPlotLogscale(axis in ['x','y'],useLoscale:Boolean) ->
  setOptions('logscale'&upper(axis),useLoscale);
setPlotPreserveAspect(preserve:Boolean) ->
  setOptions('preserveAspect',preserve);
setPlotFontsize(relativeSize>=0.1) ->
  setOptions('fontsize',relativeSize);
setPlotRangeManually(x0:Numeric,x1>x0,y0:Numeric,y1>y0) ->
  setOptions([['autoscaleX',false],
              ['autoscaleY',false],
              ['x0'        ,x0],
              ['x1'        ,x1],
              ['y0'        ,y0],
              ['y1'        ,y1]]);
setPlotRangeManually(centerX:Numeric,centerY:Numeric,range:Numeric) ->
  setOptions([['autoscaleX',false],
              ['autoscaleY',false],
              ['preserveAspect',true],
              ['x0'        ,centerX-range],
              ['x1'        ,centerX+range],
              ['y0'        ,centerY-range],
              ['y1'        ,centerY+range]]);

//*Add a tube plot based on expressions; samples are distributed linearly
addTubePlot(f0:Expression(1),f1:Expression(1),x0:Numeric,x1>x0,sampleCount>=2,style:PlotStyle)->begin
  (x0+(x1-x0)/(sampleCount-1)*[0..sampleCount-1])
  .pEach(x,[x,f0(x)],[x,f1(x)]).addPlot(style.TUBE.toString);
end;

//*Add a tube plot based on value lists
addTubePlot(x:NumericList,y0:NumericList,y1:NumericList,style:PlotStyle)->begin
  x.size==y0.size==y1.size ? void : fail('Size mismatch');
  x.each(px,[px,y0[index]],[px,y1[index]]).addPlot(style.TUBE.toString);
end;

//*Add plot showing transparent boxes from lowValues to high values with middleValues as lines
addBinPlot(binLimits:NumericList,lowValues:NumericList,middleValues:NumericList,highValues:NumericList,style:PlotStyle)->begin
  binLimits.size-1==lowValues.size==middleValues.size==highValues.size ? void : fail('Size mismatch');
  [0..lowValues.size-1].each(i,[binLimits[i],lowValues[i]],[binLimits[i+1],highValues[i]])
                       .addPlot(style.BOX.filled.withScale(0).toString);
  [0..middleValues.size-1].each(i,[binLimits[i],middleValues[i]],[binLimits[i+1],middleValues[i]],[Nan,Nan])
                          .addPlot(style.LINE.toString);
end;

//*Plot bars with labels per bar
barPlot(values:NumericList,texts:StringList,style:PlotStyle)->begin
  resetOptions;
  setPlotAxisStyle('x',false,false,false);
  setPlotAxisStyle('y',true,true,false);
  setPlotRangeManually(0,values.size,min(0,values.min),max(0,values.max));
  setPlotPreserveAspect(false);
  plot(values|0,style.toString);
  texts.each(text,drawText(index+0.5,0,text,10,'B'));
end;
