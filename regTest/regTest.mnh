USE hash;

private TEST_DIRECTORY    :=replace(splitFileName(myPath)%"relative",splitFileName(myPath)%"filename","");
private ROOT_DIRECTORY    :=TEST_DIRECTORY&'../';
private PASSED_TESTS_STORE:=TEST_DIRECTORY&'passedTests.dat';
private TESTS_STORE       :=TEST_DIRECTORY&'regTestCases.dat';

private restore->fileExists(PASSED_TESTS_STORE) ? expression(fileContents(PASSED_TESTS_STORE))() : [];
mutable passedTests:=restore;
private storePassedTests->begin
  fileExists(PASSED_TESTS_STORE) ? begin
    local oldVal:=restore;
    each(v,oldVal,v%[0,1] in passedTests%%[0,1] ? void : passedTests|=[v]);
  end : void;
  writeFile(PASSED_TESTS_STORE,replace(string(passedTests),'],[','],\n['));
end;

synchronized markAsPassed(case,exeHash,runTime)->begin passedTests|=[[exeHash,case,fileHash(case),runTime]]; void; end;
synchronized isMarkedAsPassed(case,exeHash)->[exeHash,case,fileHash(case)] in passedTests%%[0..2];

mutable testsFailed:="";
mutable previouslyPassedTests:=0;
synchronized addFailedTest(scriptName)->copy(testsFailed:=testsFailed&format("\nkdiff %X% %X%",expectedResultName(scriptName),lastResultName(scriptName)),1,0);

private expectedResultName  (scriptname)->TEST_DIRECTORY&replace(splitFileName(scriptname)%"filename"&"#",".mnh#",".expected");
private lastResultName      (scriptname)->TEST_DIRECTORY&replace(splitFileName(scriptname)%"filename"&"#",".mnh#",".last");
private expectedResultExists(scriptname)->fileExists(expectedResultName(scriptname));
private expectedResult      (scriptname)->fileLines (expectedResultName(scriptname));
private storeResult(filename,data)-> writeFileLines(filename,data) ? void : void;

runTest(executor,exeHash, scriptName,outputFileName,delOutputBefore,FMT_STRING)->
  begin
    delOutputBefore and outputFileName<>''
    ? deleteFile(ROOT_DIRECTORY&outputFileName)
    : void;
    local startTime:=systime;
    local scriptOutput:=exec(executor,["-det",ROOT_DIRECTORY&scriptName]) | (outputFileName="" ? [] : fileHash(ROOT_DIRECTORY&outputFileName));
    local scriptTime:=(systime-startTime)*24*60*60;
    expectedResultExists(scriptName)
    ? begin
        expectedResult(scriptName)==scriptOutput
        ? printf(FMT_STRING,scriptName,scriptTime,'o.k.' | markAsPassed(scriptName,exeHash,scriptTime))
        : printf(FMT_STRING,scriptName,scriptTime,"Results differ!" &
                                                   addFailedTest(scriptName) &
                                                   storeResult(lastResultName(scriptName),scriptOutput));
      end
    : printf(FMT_STRING,scriptName,scriptTime,"No reference result found. Storing "&storeResult(expectedResultName(scriptName),scriptOutput));
  end;

private average(L:numericList)->each(x,L,+)/size(L);

main('compare')->begin
  local averageTimePerTest :=each(testFile,unique(passedTests%%1),[testFile, average(passedTests%(passedTests%%1=testFile)%%3)]).sort({$x%1<=$y%1});
  print('All tests ordered by time:');
  printf('%X%\t%I%s',averageTimePerTest%%0,round(averageTimePerTest%%1,4));
  print;

  local timeVariancePerTest:=each(testFile,unique(passedTests%%1),[testFile, average(sqr(passedTests%(passedTests%%1=testFile)%%3 - averageTimePerTest%testFile))]);
  printf@('Fastest test: %X%; average time: %0.0000%s'| averageTimePerTest%(argMin(averageTimePerTest%%1)));
  local testFile:=averageTimePerTest%(argMin(averageTimePerTest%%1))%0;
  local relAvgTimePerBin:=(passedTests%(passedTests%%1=testFile)%%[0,3]).sort({$x%1<=$y%1});
  printf("%I20%\t%I%\ts",relAvgTimePerBin%%0,round(relAvgTimePerBin%%1,3));
  print;

  printf@('Slowest test: %X%; average time: %0.0000%s'| averageTimePerTest%(argMax(averageTimePerTest%%1)));
  local testFile:=averageTimePerTest%(argMax(averageTimePerTest%%1))%0;
  local relAvgTimePerBin:=(passedTests%(passedTests%%1=testFile)%%[0,3]).sort({$x%1<=$y%1});
  printf("%I20%\t%I%\ts",relAvgTimePerBin%%0,round(relAvgTimePerBin%%1,3));
  print;

  local testFile:=timeVariancePerTest%(argMax(timeVariancePerTest%%1))%0;
  printf('Test with the largest variance: %X%; average time: %0.0000%s',testFile,averageTimePerTest%testFile);
  local relAvgTimePerBin:=(passedTests%(passedTests%%1=testFile)%%[0,3]).sort({$x%1<=$y%1});
  printf("%I20%\t%I%\ts",relAvgTimePerBin%%0,round(relAvgTimePerBin%%1,3));
  print;

  local relAvgTimePerBin:=each(bin,unique(passedTests%%0),begin
    local passedByBin:=passedTests%(passedTests%%0=bin)%%[1,3]; //testFile and time
    [bin,100*(average(each(pass,passedByBin,pass%1/(averageTimePerTest%(pass%0))))-1)];
  end).sort({$x%1<=$y%1});
  print('Average relative time of executables: ');
  printf("%I20%\t%I%\t%%",relAvgTimePerBin%%0,round(relAvgTimePerBin%%1,2));
end;

main->main(executor);

main(exe,...)->begin
  testsFailed:="";
  previouslyPassedTests:=0;
  local testFilter       := ...;
  local currentExeInfo   := exe.exec(["-version"] ).head;
  local currentExeHash   := exe.exec(["-codeHash"]).head;
  local currentExeCanPlot:= copy(currentExeInfo,5,4)="full";
  local randomSample     := testFilter==['randomSample'];
  local checkOnly        := testFilter==['checkOnly'];
  local specific_tests   := not((testFilter==[]) or checkOnly or randomSample);
  checkOnly or randomSample ? testFilter:=[] : void;
  local testCases:=sort(each(case,
                   expression(fileContents(TESTS_STORE))(),
                   (currentExeCanPlot or not(case%3)) and
                   (testFilter==[] OR each(f,testFilter,OR,pos(f,case%0)>=0)) ? case%[0..2] : void));

  //count already passed tests and remove them from the list
  testCases:=each(test,testCases,
    isMarkedAsPassed(test%0,currentExeHash) and not(specific_tests)
    ? begin previouslyPassedTests+=1; void; end
    : test);
  local stopAtTime:=systime+1;
  randomSample AND size(testCases)>0 ? begin
    testCases:=testCases%sortPerm(size(testCases).random);
    stopAtTime:=systime+5/(24*60*60);
  end : void;
  local FMT_STRING:="%X"&(size(testCases)=0 ? 0 : max(length(testCases%%0)))&"% %###0.00%sec %X%";
  local first:=true;
  each(test,testCases, systime>stopAtTime ? void : begin
    checkOnly ? addFailedTest(test%0) :
    begin
      first and not(checkOnly)
      ? print('---------------------------------------------------------\n',
             'Starting test runs with executable ',exe,'\n',currentExeInfo,
             '\n---------------------------------------------------------')
      : void;
      first:=false;
      runTest@(exe|currentExeHash|test|FMT_STRING);
      void;
    end;
  end);
  first ? setErrorlevel(1) : print(testsFailed);
  testsFailed<>'' ? setErrorlevel(2) : void;

  size(testCases)>0 ? begin
    print(previouslyPassedTests,' Tests were already marked as passed.');
    storePassedTests;
    //displayUncoveredTests:--------------
    local coveredTests:=(expression(fileContents(TESTS_STORE))()%%0).unique.splitFileName%%'expanded';
    first:=true;
    each(script,
         (files(ROOT_DIRECTORY&'demos/*.mnh')|files(ROOT_DIRECTORY&'packages/*.mnh')).unique.splitFileName%%'expanded',
         script in coveredTests ? void : begin
           first ? begin
             first:=false;
             print('The following scripts are not covered by tests:');
           end : void;
           print('  ',script);
         end);
    //--------------:displayUncoveredTests
    first ? print('There are no scrips uncovered by tests') : void;
  end : void;

  checkOnly and testsFailed<>'' ? print(size(split(testsFailed,'\n')),' tests have not been passed or executed') : void;
  testsFailed='';
end;
