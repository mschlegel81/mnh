startOfEvaluation:=time;

private memoized canPlot(exe)->exe.exec(['-cmd','-echo','-time','-el5',"print('plot.plot' in listBuiltin)"])==['true'];
canPlot:='plot.plot' in listBuiltin;

private codeHash(exe)->exec(exe,['-codeHash']).head;
private codeHash:=executor.codeHash;

private memoized flavourString(exe)->exe.exec(['-version']).head.split(' ').trailing(2).reverseList.join('_')&(canPlot(exe) ? '' : '_light');
flavourString:=executor.flavourString;

private memoized logName(exe)->myPath.replace('.mnh','.'&flavourString(exe)&'.txt');
private logName:=executor.logName;

testAlreadyPassed(exe)->logName(exe).fileExists AND ((codeHash(exe)&' '&'o.k.') in logName(exe).fileLines);
testAlreadyPassed:=executor.testAlreadyPassed;

linuxTestsPassed->
  [[myPath.replace('.mnh','.Linux_x86_64.txt')      ,'FOLinux'],
   [myPath.replace('.mnh','.Linux_x86_64_light.txt'),'LOLinux']]
  .each(log,log[0].fileExists AND (((log[1])&':'&(codeHash.split(':')[1]) &' '&'o.k.') in log[0].fileLines),AND);

validateCollectedOutput->begin
  local printOut:=collectedOutput.get(collectedOutput.getInner(0)='printline').getInner(2).split("\n").flatten;
  local expectedResultName:=myPath.replace('.mnh','.'&flavourString&'.expected');
  local lastResultName    :=myPath.replace('.mnh','.'&flavourString&'.last');
  local stateMessage:='';
  clearPrint;
  collectOutput;
  print("Test finished at ",formatTime('c',systime));
  fileExists(expectedResultName)
  ? begin
      expectedResultName.fileLines==printOut
      ? stateMessage:='o.k.'
      : begin
          lastResultName.writeFileLines(printOut);
          stateMessage:='NOT OK';
          print("Result is not okay!\nPlease compare "&lastResultName&"\n           and "&expectedResultName);
        end;
    end
  : begin
      print('No expected result found. Writing ',expectedResultName);
      stateMessage:='first';
      expectedResultName.writeFileLines(printOut);
    end;
  printf('%s',exec(executor,['-version'])| (codeHash&' '&stateMessage) |("Total time: "&(time-startOfEvaluation)));
  print('');
  printOut:=collectedOutput.get(collectedOutput.getInner(0)='printline').getInner(2).split("\n").flatten;
  logName.writeFileLines(printOut | head(logName.fileLines,20));
end;
