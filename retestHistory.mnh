assertOkAndGet(execResult:List(2))->assert(execResult[1]==0) orElse execResult[0];

tagsAfterIntroductionOfFPC3:=begin
  local entries:=exec('git',['tag','--contains','6a9c9c19dbbfa4501368b6fa1e89ca45a3053a8e','--format="%(refname:short) %(objectname)"','--sort=committerdate']).assertOkAndGet.split(' ');
  local result:=[];
  entries.each(e,e[1] in result.getInner(1) ? void : result||=e);

  result;
end;

checkout(commitHash)->
  note('Checking out ',commitHash) orElse
  exec('git',['checkout','-q','-f',commitHash]).assertOkAndGet.map(::print);

buildAndTest->
  note('Testing...') orElse
  ['name'=>'make.mnh',
   'parameters'=>['clean','LOWin64!','FOWin64!'],
   'side effects'=>listSideEffects,
   'connection level'=>0].toMap.runScript;

main->tagsAfterIntroductionOfFPC3.each(tagAndRevision,begin
  local tag:=tagAndRevision[0];
  local rev:=tagAndRevision[1];
  print(formatTime(systime),' TAG: ',tag);
  checkout(rev);
  ('target/Win'&[32,64].toSet&'/mnh'&['','_light'].toSet&'.exe').filter(::fileExists).map(::deleteFile);
  buildAndTest;
  ['name'=>'make.mnh',
   'parameters'=>['status'],
   'side effects'=>listSideEffects,
   'connection level'=>0].toMap.runScript.map((x)->x[0]=='printline' ? x[2] : void).map(::print);
  ('target/Win'&[32,64].toSet&'/mnh'&['','_light'].toSet&'.exe').filter(::fileExists).map((f)->copyFile(f,f.changeFileExt('_'+tag+f.extractFileExt)));
end);
