#!/usr/bin/mnh_light -headless
barPlot(captions:List,samples:NumericList,targetWidth>0)->
barPlot(captions     ,samples            ,targetWidth,'%s');

barPlot(captions:List,samples:NumericList,targetWidth>0,sampleFormat:String)->
  begin
    local factor:=targetWidth/samples.max;
    samples.each(s,
      captions[index],
      "\t",
      format(sampleFormat,s),
      "\t",
      repeat("=",round(factor*s)),
      "\n",&);
  end;

timedExecute(exe:String,params)->begin
  print('Executing ',join(exe|params,' '));
  local t0:=scriptTime;
  exe.execPipeless(params);
  scriptTime-t0;
end;

findExecutables->allFiles('.','mnh*.exe').pFilter((executable)->begin
  local output:=executable.exec(['-info'],false);
  output[1]!=0
  ? false
  : begin
      output:=try(::interpret,[output[0]],false);
      output.isMap AND !isVoid(output['codeVersion']);
    end;
end).map((exe)->relativeFileName('.',exe)).sort;

//*Lists all available executables
main('-list')->findExecutables.map(::print);

main(...)->begin
  local timedExes:=findExecutables.each(target,[target,timedExecute(target,...)]);
  barPlot(timedExes.getInner(0),timedExes.getInner(1),100).print;
end;
