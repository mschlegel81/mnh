USE testLog,make;

currentHash:='core/code_hash.inc'.fileContents.clean(['0'..'9','A'..'F'],' ').trim;
private isLinux:=executor.exec(['-codeHash'],false).head.matches('[L|l]inux');

main('history')->begin
  local history:=testHistory;
  local codeHashes:=history
    .getInner(0)                    //all code hashes
    .group(history.getInner(1))     //group by flavors
    .each(g,g.each(e,[e,index]),|)  //attach index to elements
    .group(0,{[$x[0],$x[1]|$y[1]]}) //group by code hashes, creating a list of indexes
    .each(set,[set[0],set[1].agg(+)/set[1].size]) //transform index list to average index
    .sort(1) //sort by average index
    .getInner(0) //drop average indexes
    .reverseList; //reverse to obtain chronological order
  local flavours  :=history.getInner(1).unique;
  codeHashes.each(codeHash,
    print("\nPrevious code hash: ",codeHash) orElse
    ("Flavour\tTime\tTest result\n"&(
    flavours.each(flavour,
      history[codeHash=history.getInner(0) and flavour=history.getInner(1)].each(entry,
        flavour&"\t"&entry[2]&"\t"&(entry[3] ? 'o.k.' : 'failed')
      ).join("\n")
    ).{$x[$x<>'']}.join("\n"))).print
  );
  print;
  main;
end;

main->print('Current code hash: '&currentHash&"\nTime: "&formatTime('c',systime)&
"\n                     Binary | Exists | Version |\v Test time\n"&
"----------------------------+--------+---------+\v-------------------\n"&
((isLinux ? [0,2,4,6] : [0..7]).pEach(i,begin
  local bin:=binName(i);
  local ex:=bin.fileExists;
  local hashOk:=(bin.exec(['-codeHash'],false).head.split(':')[1])==currentHash;
  local tested:=ex AND timeForTest(bin);
  format("%27s | %6s | %7s |\v %s",bin,ex ? 'yes' : 'no',ex ? (hashOk ? 'o.k.' : 'N O K') : 'n/a' ,ex ? (tested orElse '---') : 'n/a' );
end).join("\n")));

//main('history');

