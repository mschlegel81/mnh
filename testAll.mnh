USE fileUtils;

private log:='D:\heaptrace_per_demo.txt';
private mutable failedTests:=[];

pipingBat(command:string)->begin
  local name:=tempFile('temp#.bat');
  name.writeFileLines([command&' 2>> '&log]);
  local code:='cmd'.execPipeless(['/C',name]);
  code==0
  ? void
  : failedTests|=command;
  deleteFile(name);
end;

mnhParameter(demo)->
  demo.matches('htmlFunctionPlotter') OR demo.matches('timer') ? ' ' :
  ' -vP3 ';
demoParamter(demo)->
  demo.matches('24\.mnh'  ) ? ' demo' :
  demo.matches('diff\.mnh') ? ' test' :
  demo.matches('emptyFolders\.mnh' ) OR demo.matches('findDuplicates\.mnh')  ? ' .' :
  demo.matches('grep\.mnh') ? ' group demos/*.mnh' :
  '';

main->begin
  local tasks:=files('demos/*.mnh')
    .each(demo,executor&mnhParameter(demo)&demo.splitFileName['expanded']&demoParamter(demo));
  tasks.each(task,begin
    index=0
    ? log.writeFileLines([task])
    : log.appendFileLines([task]);
    pipingBat(task);
  end);
  failedTests==[]
  ? print('All runs terminated with status 0')
  : begin
      print('Tests with nonzero exit status: ');
      printf('  %s',failedTests);
    end;
end;
